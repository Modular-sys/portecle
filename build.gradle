import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'org.beryx.runtime' version '1.12.5'
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}
group 'net.sf'
version '0.0.1'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

application {
    mainClass = "net.sf.portecle.FPortecle"
    applicationName = 'portecle'
}

dependencies {
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

tasks.jar.enabled = true

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    modules = ['java.management', 'jdk.management', 'java.naming', 'java.instrument', 'java.sql', 'jdk.unsupported', 'jdk.security.jgss', 'java.desktop']

    launcher {
        noConsole = false

        unixScriptTemplate = file('src/main/script/unixScriptTemplate.txt')
        windowsScriptTemplate = file('src/main/script/windowsScriptTemplateJavaw.txt')

        jvmArgs = [
                '-Dfile.encoding=UTF-8'
        ]
    }

    jpackage {

        OperatingSystem os = OperatingSystem.current()
        String imgType = os.windows ? 'ico' : os.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', "src/main/resources/portecle.$imgType"]
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'Acme Corporation']

        if (os.windows) {
            installerType = 'msi'
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        } else if (os.linux) {
            installerType = "rpm"
            installerOptions += ['--linux-package-name', 'portecle', '--linux-shortcut']
        } else if (os.macOsX) {
            installerOptions += ['--mac-package-name', 'portecle']
        }
    }
}